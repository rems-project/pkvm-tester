#!/bin/zsh
set -e

# This is meant to be run right after executing tests.
#
# It consumes *.gcda files (produced during the run), and *.gcno files (produced
# as part of the build), and produces lcov *.info files.
#
# Since the *.gcno must match the *.gcda, the build directory needs to be in the
# same state it was when building the corresponding kernel — running make in
# between can generate *.gcno files incompatible with the *.gcda files that the
# running kernel produces.
# 
# Usage: ${0} OUTPUT-DIR

lcovargs=(
  -q
  --branch-coverage
  --ignore-errors inconsistent 
)
lcovcap=(
  --gcov-tool=llvm-cov,gcov 
)

eecho() { echo $@ >&2; }
output="$1"
[[ -d $output ]] || { eecho "$output: not a dir"; exit 1; }
[[ -x =lcov ]] || { eecho "Cannot find `lcov` — not installed?"; exit 1; }
[[ -x =llvm-cov ]] || { eecho "Cannot find `llvm-cov` — not installed?"; exit 1; }

gcovs=( $output/gcov-*.tar.gz(N) )
(( $#gcovs == 0 )) && exit 0

tmp=$(mktemp -d --suff="-gcov") || exit 127

for gcov in $gcovs; do
  echo "LCOV: Capturing: ${gcov:t}"
  dir=$tmp/${gcov:t:r:r}
  mkdir $dir
  tar xf $gcov -C $dir
  links=( $dir/**/arch/arm64/**/*.c(@:A) )
  base=$(sed -e 's:/arch/arm64/.*::' <<< $links[1])
  lcov $lcovargs $lcovcap --capture --all -d $dir -b $base -o ${gcov:r:r}.info
done

[[ -d $tmp ]] && rm -rf $tmp

echo "LCOV: Combining: aggregate.info"
lcov $lcovargs --add-tracefile=${^gcovs:r:r}.info -o $output/gcov-99-aggregate.info
