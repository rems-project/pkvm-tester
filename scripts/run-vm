#!/bin/zsh
set -e

if [[ $(uname) == "Darwin" ]]; then
  DATE=$(date -Iseconds)
else
  DATE=$(date --iso-8601=seconds)
fi

cpus=1 mem=2G

out="${0:h:h}/output/$(uname -n)-$DATE"
root=${0:h:h}/_build
efidisk=${root}/efi.img
vardisk=${root}/var.img

params=(
  --name "pkvm-test-runner"
  --machine virt,virtualization=true,gic-version=3
  --smp $cpus -m $mem --cpu cortex-a72
  --drive if=pflash,file=${efidisk},format=raw,readonly=on
  --drive if=pflash,file=${vardisk},format=raw
  --virtfs local,path=$out,mount_tag=output,security_model=none
  --nographic
  --no-reboot  # Exit instead of rebooting
  --initrd ${root}/initramfs.img
)

# linux/Documentation/admin-guide/kernel-parameters.txt:
append=(
  panic=-1  # Reboot immediately.
  nokaslr   # Disable address space layout randomization.
  quiet     # Suppress kernel outputs.
  kvm-arm.mode=protected  # pKVM.
)

[[ -v DEBUG ]] && params+=(-S --gdb tcp::${DEBUG:-1234})

mkdir -p $out
make -C ${0:h:h} images

qemu-system-aarch64 $params --append "$append" $@ | tee "$out/serial"
